plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.3"
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
    id 'net.neoforged.gradle.userdev' version '7.0.170'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

repositories {
    mavenLocal()
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://mvn.devos.one/snapshots" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
    maven { url = "https://cursemaven.com" }
}

dependencies {
    implementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    implementation("net.createmod.ponder:Ponder-NeoForge-${minecraft_version}:${ponder_version}")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:${flywheel_version}")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_version}")
    implementation("com.tterrag.registrate:Registrate:${registrate_version}")
    implementation("curse.maven:cold_sweat-506194:6176789")
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        argument '--nogui'
    }
    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        arguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

publishMods {
    file = jar.archiveFile
    changelog = file("CHANGELOG.md").text
    type = STABLE
    modLoaders.add("forge")

    curseforge {
        projectId = "1184450"
        projectSlug = "create-cold-sweat"
        accessToken = providers.gradleProperty("CURSEFORGE_TOKEN")
        minecraftVersions.add("1.20.1")
        minecraftVersions.add("1.20.2")
        minecraftVersions.add("1.20.3")
        minecraftVersions.add("1.20.4")
        minecraftVersions.add("1.20.5")
        minecraftVersions.add("1.20.6")
        requires("create")
        requires("cold-sweat")
    }
    modrinth {
        projectId = "Arx7g8V4"
        accessToken = providers.gradleProperty("MODRINTH_TOKEN")
        minecraftVersions.add("1.20.1")
        minecraftVersions.add("1.20.2")
        minecraftVersions.add("1.20.3")
        minecraftVersions.add("1.20.4")
        minecraftVersions.add("1.20.5")
        minecraftVersions.add("1.20.6")
    }
    discord {
        webhookUrl = providers.gradleProperty("WEBHOOK_TOKEN")
        username = "StarWorks Mods"
        avatarUrl = "https://cdn.discordapp.com/icons/1376617717846638703/2119cf72f3d468a689199b0979544cb5.png?size=80&quality=lossless"
    }
}
